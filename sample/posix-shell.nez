// Nez posix shell grammar
// (see http://pubs.opengroup.org/onlinepubs/009695399/utilities/xcu_chap02.html)
// =======================

// entry point
W = [a-zA-Z]
_ = [ \t]*


File = CompleteCommand

// ##############################
// ##     grammar symbols      ##
// ##############################

// operator
AND_IF    = '&&'
OR_IF     = '||'
DSEMI     = ';;'
DLESS     = '<<'
DGREAT    = '>>'
LESSAND   = '<&'
GREATAND  = '>&'
LESSGREAT = '<>'
DLESSDASH = '<<-'
CLOBBER   = '>|'

// reserved words
IF    = 'if'    !W
THEN  = 'then'  !W
ELSE  = 'else'  !W
ELIF  = 'elif'  !W
FI    = 'fi'    !W
DO    = 'do'    !W
DONE  = 'done'  !W
CASE  = 'case'  !W
ESAC  = 'esac'  !W
WHILE = 'while' !W
UNTIL = 'until' !W
FOR   = 'for'   !W

// reserved word in some context
LBRACE = '{'
RBRACE = '}'
BANG   = '!'
IN     = 'in' !W

KEYWORD
    = IF / THEN / ELSE / ELIF / FI / DO
    / DONE / CASE / ESAC / WHILE / UNTIL / FOR
    / IN

// name or word //FIXME:
NAME = [_a-zA-Z][_a-zA-Z0-9]*
WORD = NAME

IO_NUMBER = [0-9] ![0-9]
NEWLINE = [\n\r]
ASSIGNMENT_WORD = '='


// #####################
// ##     grammar     ##
// #####################

CompleteCommand
    = _ List Separator?

List
    = AndOr (SeparatorOp AndOr)*

AndOr
    = Pipeline ((AND_IF / OR_IF) LineBreak Pipeline)*

Pipeline
    = (BANG _)? PipeSequence

PipeSequence
    = Command ( _ '|' LineBreak Command)*

Command
    = SimpleCommand
    / CompoundCommand RedirectList?
    / FunctionDefinition

CompoundCommand
    = BraceGroup
    / Subshell
    / ForClause
    / CaseClause
    / IfClause
    / WhileClause
    / UntilClause

Subshell
    = '(' _ CompoundList _ ')'

CompoundList
    = NewlineList? Term Separator?

Term
    = AndOr (Separator AndOr)*

ForClause
    = FOR _ Name LineBreak (IN _ WordList? SequentialSep)? DoGroup

Name
    = [_a-zA-Z] [_a-zA-Z0-9]*

WordList
    = WORD+

CaseClause
    = CASE WORD LineBreak IN _ LineBreak (CaseList / CaseListNS)? ESAC

CaseListNS
    = CaseList? _ CaseItemNS

CaseList
    = CaseItem ( _ CaseItem)*

CaseItemNS
    = '('? _ Pattern _ ')' _ CompoundList? LineBreak

CaseItem
    = '('? _ Pattern _ ')' _ (LineBreak / CompoundList) _ DSEMI LineBreak

Pattern
    = WORD ( _ '|' _ WORD)*

IfClause
    = IF _ CompoundList _ THEN _ CompoundList _ ElsePart? FI

ElsePart
    = ELIF _ CompoundList _ THEN _ ElsePart
    / ELSE _ CompoundList _

WhileClause
    = WHILE _ CompoundList _ DoGroup

UntilClause
    = UNTIL _ CompoundList _ DoGroup

FunctionDefinition
    = Fname _ '(' _ ')' LineBreak FunctionBody

FunctionBody
    = CompoundCommand RedirectList? //FIXME: not allow word expansion

Fname
    = NAME

BraceGroup
    = LBRACE _ CompoundList _ RBRACE

DoGroup
    = DO _ CompoundList _ DONE

SimpleCommand
    = CmdPrefix (CmdWord? CmdSuffix)?
    / CmdName CmdSuffix?

CmdName
    = !KEYWORD ( '\\' [= \t\n\r] / ![= \t\n\r] .)+

CmdWord
    = WORD //FIXME:

CmdPrefix
    = (IoRedirect / ASSIGNMENT_WORD)+

CmdSuffix
    = (IoRedirect / WORD)+

RedirectList
    = IoRedirect+

IoRedirect
    = IO_NUMBER? (IoFile / IoHere)

IoFile
    = '<'       FileName
    / LESSAND   FileName
    / '>'       FileName
    / GREATAND  FileName
    / DGREAT    FileName
    / LESSGREAT FileName
    / CLOBBER   FileName

FileName
    = WORD //FIXME:

IoHere
    = DLESS HereEnd
    / DLESSDASH HereEnd

HereEnd
    = WORD //FIXME:

NewlineList
    = _ (NEWLINE _)+

LineBreak
    = NewlineList?

SeparatorOp
    = _ ('&' / ';') _

Separator
    = SeparatorOp LineBreak
    / NewlineList

SequentialSep
    = _ ';' LineBreak
    / NewlineList
